plugins {
    id 'fabric-loom' version '1.11-SNAPSHOT' apply false
}

subprojects {
    apply plugin: 'fabric-loom'
    apply plugin: 'maven-publish'

    version = project.mod_version
    group = project.maven_group

    base {
        def moduleName = project.name.replace('src:', '')
        archivesName = project.archives_base_name + "-" + moduleName
    }

    repositories {
        mavenCentral()
        maven { url "https://maven.fabricmc.net/" }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

        // 所有模块都需要的通用依赖
        implementation 'redis.clients:jedis:4.4.0'
    }

    processResources {
        inputs.property "version", project.version
        inputs.property "minecraft_version", project.minecraft_version
        inputs.property "loader_version", project.loader_version
        filteringCharset "UTF-8"

        filesMatching("fabric.mod.json") {
            expand "version": project.version,
                    "minecraft_version": project.minecraft_version,
                    "loader_version": project.loader_version
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"
        it.options.release = 21
    }

    java {
        withSourcesJar()
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
}